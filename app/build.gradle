// Android plugin
apply plugin: 'com.android.application'

// Kotlin plugin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    // Auto version numbering
    def versionPropsFile = file('version.properties')
    def versionMajor, versionMinor, versionRevision, versionBuild, versionCodeNr
    if (!versionPropsFile.canRead()) {
        throw new FileNotFoundException("Could not read version.properties!")
    } else {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        versionMajor = versionProps['VERSION_MAJOR'].toInteger()
        versionMinor = versionProps['VERSION_MINOR'].toInteger()
        versionRevision = versionProps['VERSION_REVISION'].toInteger()
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
        versionCodeNr = versionProps['VERSION_CODE'].toInteger()

        // build number gets incremented every run
        versionBuild = versionBuild + 1
        if (!versionPropsFile.canWrite()) {
            throw new FileNotFoundException("Could not write version.properties!")
        } else {
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(new FileOutputStream(versionPropsFile), "writing new versions")
        }
    }
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        applicationId "me.kaesaecracker.campus_dual"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionCodeNr
        versionName "$versionMajor.$versionMinor.$versionRevision.$versionBuild"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


dependencies {
    // google and kotlin stuff
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.23.3'

    // My libraries
    implementation 'me.eugeniomarletti:android-extras-delegates:1.0.5'
    implementation 'com.github.kittinunf.fuel:fuel-android:1.15.1'
    implementation 'com.google.code.gson:gson:2.8.5'
}

apply plugin: 'com.google.gms.google-services'

kotlin {
    experimental {
        coroutines "enable"
    }
}