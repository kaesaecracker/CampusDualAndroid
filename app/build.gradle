// Android plugin
apply plugin: 'com.android.application'

// Kotlin plugin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: "androidx.navigation.safeargs"

android {
    compileSdkVersion 28
    // Auto version numbering
    def versionPropsFile = file('version.properties')
    if (!versionPropsFile.canRead()) {
        throw new FileNotFoundException("Could not read version.properties!")
    }

    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    def versionMajor = versionProps['VERSION_MAJOR'].toInteger()
    def versionMinor = versionProps['VERSION_MINOR'].toInteger()
    def versionRevision = versionProps['VERSION_REVISION'].toInteger()
    def versionCodeNr = versionProps['VERSION_CODE'].toInteger()
    def versionBeta = versionProps['VERSION_BETA'].toInteger()

    defaultConfig {
        applicationId 'xyz.mattishub.campusDual'
        minSdkVersion 21
        targetSdkVersion 28

        versionCode versionCodeNr
        def versionNameBetaPart = ""
        if (versionBeta > -1) {
            versionNameBetaPart += "-beta"
            if (versionBeta > 0)
                versionNameBetaPart += ".$versionBeta"
        }
        versionName "$versionMajor.$versionMinor.$versionRevision$versionNameBetaPart ($versionCodeNr)"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
            useProguard false
        }

        release {
            minifyEnabled true
            debuggable false
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def ktxCoroutinesVer = "1.0.1"
    def anxNavigationVer = "1.0.0"
    def fuelVer = "2.0.1"

    // google and kotlin stuff
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.material:material:1.0.0'
    
    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$ktxCoroutinesVer"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$ktxCoroutinesVer"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$ktxCoroutinesVer"

    // background workers
    implementation 'android.arch.work:work-runtime-ktx:1.0.0'

    // navigation
    implementation "android.arch.navigation:navigation-fragment-ktx:$anxNavigationVer"
    implementation "android.arch.navigation:navigation-ui-ktx:$anxNavigationVer"

    // jetpack
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    // My libraries
    implementation "com.github.kittinunf.fuel:fuel:$fuelVer"
    implementation "com.github.kittinunf.fuel:fuel-android:$fuelVer"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'joda-time:joda-time:2.10.1'

    // leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.3'
}
